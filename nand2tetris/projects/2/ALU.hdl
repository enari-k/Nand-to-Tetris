// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a = x,b = false,sel = zx,out = a1);
    Not16(in = a1,out = a2);
    Mux16(a = a1,b = a2,sel = nx,out = a4);
    Mux16(a = y,b = false,sel = zy,out = b1);
    Not16(in = b1,out = b2);
    Mux16(a = b1,b = b2,sel = ny,out = b4);
    And16(a=a4, b=b4, out=ab1);
    Add16(a=a4, b=b4, out=ab2);
    Mux16(a=ab1, b=ab2, sel=f, out=ab3);
    Not16(in = ab3,out = ab4);
    Mux16(a=ab3, b=ab4, sel=no, out=out,out[15]=ng , out[0..7] = low8 , out[8..15]= high8);
    Or8Way(in=low8, out=orLow);
    Or8Way(in=high8,out=orhigh);
    Or(a = orLow,b = orhigh,out = zr1);
    Not(in = zr1,out = zr);
}